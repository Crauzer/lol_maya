global proc int SKNTranslatorOpts (string $parent, string $action, string $initialSettings, string $resultCallback)
{
	if ($action == "post") {
	    setParent $parent;
	    formLayout form;
        checkBox -label "Import skeleton: load SKL in same directory and bind with SKN as skin cluster." skl;
        formLayout -e -attachPosition skl left 0 0 -attachPosition skl right 0 0 -attachPosition skl top 0 0 form;
        checkBox -label "Import mesh separated by material." sepmat;
        formLayout -e -attachPosition sepmat left 0 0 -attachPosition sepmat right 0 0 -attachPosition sepmat top 0 60 form;
                    
        string  $options[];
        string	$option[];
        tokenize($initialSettings, ";", $options);
        for ($index = 0; $index < size($options); $index++) {
		    tokenize($options[$index], "=", $option);
            if ($option[0] == "skl") {
                if ($option[1] == "0") 
                    checkBox -edit -value 0 skl;
                else 
                    checkBox -edit -value 1 skl;
            }
            else if ($option[0] == "sepmat") {
                if ($option[1] == "0") 
                    checkBox -edit -value 0 sepmat;
                else 
                    checkBox -edit -value 1 sepmat;
            }
        }
		return 1;
	} 
	else if ($action == "query") {
		string	$options;
        if (`checkBox -q -value skl` == 0) 
            $options = "skl=0";
        else
            $options = "skl=1";
            
        if (`checkBox -q -value sepmat` == 0) 
            $options = $options + ";sepmat=0";
        else
            $options = $options + ";sepmat=1";
        eval($resultCallback+" \""+$options+"\"");
        return 1;
	} 
	return 0;
}